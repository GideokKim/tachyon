load("@bazel_skylib//rules:common_settings.bzl", "string_flag")
load("//bazel:tachyon.bzl", "if_aarch64", "if_has_avx512", "if_x86_64")
load("//bazel:tachyon_cc.bzl", "tachyon_avx512_defines", "tachyon_cc_library")
load("//tachyon/math/finite_fields/generator/prime_field_generator:build_defs.bzl", "SUBGROUP_GENERATOR", "generate_fft_prime_fields")

package(default_visibility = ["//visibility:public"])

string_flag(
    name = SUBGROUP_GENERATOR,
    build_setting_default = "3",
)

generate_fft_prime_fields(
    name = "koala_bear",
    class_name = "KoalaBear",
    # 2³¹ - 2²⁴ + 1
    # Hex: 0x7f000001
    modulus = "2130706433",
    namespace = "tachyon::math",
    subgroup_generator = ":" + SUBGROUP_GENERATOR,
    use_montgomery = True,
)

tachyon_cc_library(
    name = "koala_bear4",
    hdrs = ["koala_bear4.h"],
    deps = [
        "//tachyon/math/finite_fields/koala_bear/internal:koala_bear4",
        "//tachyon/math/finite_fields/koala_bear/internal:packed_koala_bear4",
    ],
)

tachyon_cc_library(
    name = "packed_koala_bear",
    hdrs = ["packed_koala_bear.h"],
    defines = tachyon_avx512_defines(),
    deps = [
        ":packed_koala_bear_neon",
        "//tachyon/build:build_config",
        "//tachyon/math/finite_fields:extended_packed_field_traits_forward",
        "//tachyon/math/finite_fields:finite_field_traits",
        "//tachyon/math/matrix:prime_field_num_traits",
    ] + if_has_avx512(
        [":packed_koala_bear_avx512"],
        [":packed_koala_bear_avx2"],
    ),
)

tachyon_cc_library(
    name = "packed_koala_bear4",
    hdrs = ["packed_koala_bear4.h"],
    deps = [
        "//tachyon/math/finite_fields:extended_packed_field_traits_forward",
        "//tachyon/math/finite_fields/koala_bear/internal:koala_bear4",
        "//tachyon/math/finite_fields/koala_bear/internal:packed_koala_bear4",
    ],
)

tachyon_cc_library(
    name = "packed_koala_bear_avx2",
    srcs = if_x86_64(["packed_koala_bear_avx2.cc"]),
    hdrs = if_x86_64(["packed_koala_bear_avx2.h"]),
    copts = if_x86_64(["-mavx2"]),
    deps = [
        ":koala_bear",
        "//tachyon:export",
        "//tachyon/math/finite_fields:packed_prime_field32_avx2",
        "//tachyon/math/finite_fields:packed_prime_field_base",
    ],
)

tachyon_cc_library(
    name = "packed_koala_bear_avx512",
    srcs = if_x86_64(["packed_koala_bear_avx512.cc"]),
    hdrs = if_x86_64(["packed_koala_bear_avx512.h"]),
    copts = if_x86_64([
        "-mavx512f",
        # NOTE(chokobole): See https://gitlab.com/libeigen/eigen/-/blob/0b51f763cbbd0ed08168f88972724329f0375498/Eigen/src/Core/util/ConfigureVectorization.h#L248.
        "-mfma",
    ]),
    deps = [
        ":koala_bear",
        "//tachyon:export",
        "//tachyon/math/finite_fields:packed_prime_field32_avx512",
        "//tachyon/math/finite_fields:packed_prime_field_base",
    ],
)

tachyon_cc_library(
    name = "packed_koala_bear_neon",
    srcs = if_aarch64(["packed_koala_bear_neon.cc"]),
    hdrs = if_aarch64(["packed_koala_bear_neon.h"]),
    deps = [
        ":koala_bear",
        "//tachyon:export",
        "//tachyon/math/finite_fields:packed_prime_field32_neon",
        "//tachyon/math/finite_fields:packed_prime_field_base",
    ],
)

tachyon_cc_library(
    name = "poseidon2",
    hdrs = ["poseidon2.h"],
    deps = [
        ":koala_bear",
        "//tachyon/base/types:always_false",
    ],
)
