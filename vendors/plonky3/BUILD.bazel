load("@crate_index//:defs.bzl", "aliases", "all_crate_deps")
load("@cxx.rs//tools/bazel:rust_cxx_bridge.bzl", "rust_cxx_bridge")
load("@local_config_cuda//cuda:build_defs.bzl", "if_cuda")
load("//bazel:tachyon.bzl", "if_gpu_is_configured", "if_has_openmp_on_macos")
load("//bazel:tachyon_cc.bzl", "tachyon_cc_library", "tachyon_openmp_linkopts")
load("//bazel:tachyon_rust.bzl", "tachyon_rust_library", "tachyon_rust_test")

FEATURES = if_gpu_is_configured(["gpu"])

tachyon_rust_library(
    name = "plonky3",
    srcs = glob(["src/**/*.rs"]),
    aliases = aliases(),
    crate_features = FEATURES,
    proc_macro_deps = all_crate_deps(proc_macro = True),
    deps = all_crate_deps(normal = True) + [
        ":baby_bear_cxx_bridge",
        ":baby_bear_poseidon2_cxx_bridge",
        ":baby_bear_poseidon2_duplex_challenger",
        ":baby_bear_row_major_matrix",
        "//tachyon/rs:tachyon_rs",
    ],
)

# NOTE(chokobole): This attribute could be added to `plonky3_test`,
# but this approach doesn't work when compiling with nvcc.
# rustc_flags = if_has_openmp(["-lgomp"]),
cc_library(
    name = "openmp",
    linkopts = tachyon_openmp_linkopts(),
)

tachyon_rust_test(
    name = "plonky3_test",
    size = "small",
    aliases = aliases(),
    crate = ":plonky3",
    crate_features = FEATURES,
    proc_macro_deps = all_crate_deps(proc_macro_dev = True),
    deps = all_crate_deps(normal_dev = True) + [
        ":openmp",
        "@local_config_gmp//:gmp",
    ] + if_cuda([
        "@local_config_cuda//cuda:cudart_static",
    ]) + if_has_openmp_on_macos([
        "@local_config_omp//:omp",
    ]),
)

rust_cxx_bridge(
    name = "baby_bear_poseidon2_cxx_bridge",
    src = "src/baby_bear_poseidon2.rs",
    args = if_gpu_is_configured([
        '--cfg=feature="gpu"',
    ]),
    deps = [":baby_bear_poseidon2_api_hdrs"],
)

rust_cxx_bridge(
    name = "baby_bear_cxx_bridge",
    src = "src/baby_bear.rs",
    args = if_gpu_is_configured([
        '--cfg=feature="gpu"',
    ]),
    deps = [":baby_bear_api_hdrs"],
)

tachyon_cc_library(
    name = "baby_bear_poseidon2_api_hdrs",
    hdrs = ["include/baby_bear_poseidon2_duplex_challenger.h"],
    deps = [
        "//tachyon/c/zk/air/plonky3/challenger:baby_bear_poseidon2_duplex_challenger",
        "@cxx.rs//:core",
    ],
)

tachyon_cc_library(
    name = "baby_bear_api_hdrs",
    hdrs = ["include/baby_bear_row_major_matrix.h"],
    deps = [
        "//tachyon/c/math/matrix:baby_bear_row_major_matrix",
        "@cxx.rs//:core",
    ],
)

tachyon_cc_library(
    name = "baby_bear_poseidon2_duplex_challenger",
    srcs = ["src/baby_bear_poseidon2_duplex_challenger.cc"],
    deps = [
        ":baby_bear_poseidon2_api_hdrs",
        ":baby_bear_poseidon2_cxx_bridge/include",
    ],
)

tachyon_cc_library(
    name = "baby_bear_row_major_matrix",
    srcs = ["src/baby_bear_row_major_matrix.cc"],
    deps = [
        ":baby_bear_api_hdrs",
        ":baby_bear_cxx_bridge/include",
    ],
)
